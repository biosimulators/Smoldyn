# python binding.
find_package(Python3 COMPONENTS Interpreter Development REQUIRED)
set(PYTHON_EXECUTABLE ${Python3_EXECUTABLE})
set(PYTHON_INCLUDE_DIRS ${Python3_INCLUDE_DIRS})

# NOTE: DO NOT change this without changing PYTHONPATH in
# ../examples/CMakeLists.txt file.
set(PY_SMOLDYN_OUTDIR ${CMAKE_BINARY_DIR}/py)
file(MAKE_DIRECTORY ${PY_SMOLDYN_OUTDIR})

# recompile libsmoldyn for PYTHON.
set_source_files_properties(${SRC_FILES} PROPERTIES LANGUAGE CXX )
add_library(_pysmoldyn STATIC ${SRC_FILES})
target_include_directories(_pysmoldyn PRIVATE ${PYTHON_INCLUDE_DIRS} ${PYBIND11_SOURCE_DIR}/include)
target_compile_options(_pysmoldyn PRIVATE -DCOMPILE_AS_PY_MODULE -DENABLE_PYTHON_CALLBACK -fvisibility=hidden)
set_target_properties(_pysmoldyn PROPERTIES POSITION_INDEPENDENT_CODE ON)
target_link_libraries(_pysmoldyn PRIVATE ${DEP_LIBS})
target_link_libraries(_pysmoldyn PUBLIC ${PYTHON_LIBRARIES})

pybind11_add_module(_smoldyn MODULE CallbackFunc.cpp Smoldyn.cpp module.cpp)
target_compile_options(_smoldyn PRIVATE 
	-DSMOLDYN_VERSION=${SMOLDYN_VERSION} 
	-DENABLE_PYTHON_CALLBACK 
	# https://pybind11.readthedocs.io/en/stable/faq.html#someclass-declared-with-greater-visibility-than-the-type-of-its-field-someclass-member-wattributes
	-fvisibility=hidden)
target_include_directories(_smoldyn PRIVATE 
    ${PYTHON_INCLUDE_DIRS}
    ${CMAKE_SOURCE_DIR}/source/external/pybind11/include)

# If generator pattern is not used here, cmake produces module in Release/Debug folders
# which will break the wheels.
set_target_properties(_smoldyn PROPERTIES
    LIBRARY_OUTPUT_DIRECTORY $<1:${PY_SMOLDYN_OUTDIR}/smoldyn/>)

add_custom_target(copy_python_tree 
  COMMAND ${CMAKE_COMMAND} -E copy_directory
    ${CMAKE_SOURCE_DIR}/source/python/smoldyn ${PY_SMOLDYN_OUTDIR}/smoldyn
  COMMENT "Copying python source tree to binary directory"
  VERBATIM)

add_dependencies(_smoldyn _pysmoldyn copy_python_tree)
target_link_libraries(_smoldyn PRIVATE _pysmoldyn)


# copy setup.py.in to setup.py
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/setup.py.in
    ${PY_SMOLDYN_OUTDIR}/setup.py)

# Wheel is the main python target. It is generated by default.
add_custom_target(wheel ALL 
    DEPENDS _smoldyn
    COMMAND ${PYTHON_EXECUTABLE} setup.py bdist_wheel -d ${CMAKE_BINARY_DIR} 
    WORKING_DIRECTORY ${PY_SMOLDYN_OUTDIR}
    COMMENT "Generating wheel in ${CMAKE_BINARY_DIR}"
    VERBATIM)

# This target is used by install script (useful in packaging).
add_custom_target(pyinstall
    COMMAND ${PYTHON_EXECUTABLE} setup.py install 
      --prefix ${CMAKE_INSTALL_PREFIX}
    WORKING_DIRECTORY ${PY_SMOLDYN_OUTDIR}
    COMMENT "RUNNING: python3 setup.py install --prefix ${CMAKE_INSTALL_PREFIX}"
    VERBATIM)

add_custom_target(pyinstall_venv
    DEPENDS _smoldyn
    COMMAND ${PYTHON_EXECUTABLE} setup.py install
    WORKING_DIRECTORY ${PY_SMOLDYN_OUTDIR}
    COMMENT "RUNNING: python3 setup.py install"
    VERBATIM)

add_custom_target(pyuninstall
    COMMAND ${PYTHON_EXECUTABLE} -m pip uninstall smoldyn
    COMMENT "RUNNING: `pip uninstall smoldyn`"
    VERBATIM)

add_custom_target(pydevel
    DEPENDS _smoldyn
    COMMAND ${PYTHON_EXECUTABLE} setup.py develop --user
    WORKING_DIRECTORY ${PY_SMOLDYN_OUTDIR}
    COMMENT "RUNNING: python3 setup.py develop --user"
    VERBATIM)

add_custom_target(doc_api_html
    DEPENDS _smoldyn
    COMMAND sphinx-build ${CMAKE_CURRENT_SOURCE_DIR}/docs/source
        ${PY_SMOLDYN_OUTDIR}/html
    WORKING_DIRECTORY ${PY_SMOLDYN_OUTDIR}
    COMMENT "Generating Python's API doc (HTML)"
    VERBATIM)

# Run static type checking using mypy (developer only). Works best with
# python3.7+
add_custom_target(lint 
    COMMAND mypy ${CMAKE_SOURCE_DIR}/source/python/smoldyn/*.py
    WORKING_DIRECTORY ${CMAKE_BINARY_DIR})

add_custom_target(lint_strict
    COMMAND mypy --strict ${CMAKE_SOURCE_DIR}/source/python/smoldyn/*.py
    WORKING_DIRECTORY ${CMAKE_BINARY_DIR})


###  python tests ###
enable_testing()
file(GLOB TEST_SCRIPTS "${CMAKE_SOURCE_DIR}/tests/test_*.py")
foreach(_pyscript ${TEST_SCRIPTS})
    # message(STATUS "Adding test ${_pyscript}") 
    get_filename_component(_test_name ${_pyscript} NAME_WE)
    get_filename_component(_test_dir ${_pyscript} DIRECTORY)
    add_test(NAME ${_test_name} COMMAND 
        ${PYTHON_EXECUTABLE} ${_pyscript}
        WORKING_DIRECTORY ${_test_dir})
    set_tests_properties(${_test_name} PROPERTIES 
        ENVIRONMENT "PYTHONPATH=${PY_SMOLDYN_OUTDIR}")
endforeach()

add_test(NAME pytest 
    COMMAND 
    ${PYTHON_EXECUTABLE} -m pytest --ignore-glob source/pybind11-*
    ${CMAKE_SOURCE_DIR}
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR})
set_tests_properties(pytest 
    PROPERTIES ENVIRONMENT "PYTHONPATH=${PY_SMOLDYN_OUTDIR};SMOLDYN_NO_PROMPT=1")
